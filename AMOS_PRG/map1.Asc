Screen Open 0,320,200,8,Lowres : Flash Off : Curs Off 
Double Buffer : Autoback 1  
Hide On

MAP_WIDTH=40 : MAP_HEIGHT=25
_VIEW_WIDTH=12 : _VIEW_HEIGHT=8
Dim MAP(MAP_WIDTH,MAP_HEIGHT)

CAM_MAX_X = MAP_WIDTH*16 - 320
CAM_MAX_Y = MAP_HEIGHT*16 - 200

Global PL_X,PL_Y,PL_SPEED,CAM_X,CAM_Y
PL_X=100 : PL_Y=100
PL_SPEED=2
CAM_X=0 : CAM_Y=0

Gosub _CREATE_MAP
Gosub _CREATE_TILES

' Crear el sprite del jugador (tile 5)
Ink 7,0 : Bar 0,0 To 15,15
Get Sprite 1,0,0 To 15,15
Sprite Off : Cls 0

Repeat 
   ' Movimiento jugador
   If Jright(1) Then PL_X=PL_X+PL_SPEED
   If Jleft(1)  Then PL_X=PL_X-PL_SPEED
   If Jup(1)    Then PL_Y=PL_Y-PL_SPEED
   If Jdown(1)  Then PL_Y=PL_Y+PL_SPEED

   ' Límites jugador
   If PL_X<0 Then PL_X=0
   If PL_X>MAP_WIDTH*16-1 Then PL_X=MAP_WIDTH*16-1
   If PL_Y<0 Then PL_Y=0
   If PL_Y>MAP_HEIGHT*16-1 Then PL_Y=MAP_HEIGHT*16-1

   ' Pos cámara relativa al jugador
   _SPR_X=PL_X-CAM_X : _SPR_Y=PL_Y-CAM_Y

   If _SPR_X>180 And CAM_X<CAM_MAX_X Then CAM_X=CAM_X+PL_SPEED
   If _SPR_X<140 And CAM_X>0        Then CAM_X=CAM_X-PL_SPEED
   If _SPR_Y>120 And CAM_Y<CAM_MAX_Y Then CAM_Y=CAM_Y+PL_SPEED
   If _SPR_Y<80 And CAM_Y>0         Then CAM_Y=CAM_Y-PL_SPEED

   ' Límites cámara
   If CAM_X<0 Then CAM_X=0
   If CAM_X>CAM_MAX_X Then CAM_X=CAM_MAX_X
   If CAM_Y<0 Then CAM_Y=0
   If CAM_Y>CAM_MAX_Y Then CAM_Y=CAM_MAX_Y

   Gosub _DRAW_MAP

   Sprite 1,_SPR_X,_SPR_Y,1

   Wait Vbl

Until Key State(69)=True

' ----------------------------

_DRAW_MAP:
   _START_X=CAM_X/16 : _START_Y=CAM_Y/16
   OFFSET_X=CAM_X Mod 16 : OFFSET_Y=CAM_Y Mod 16

_END_X = _START_X + _VIEW_WIDTH + 2
_END_Y = _START_Y + _VIEW_HEIGHT + 2

   If _START_X<0 Then _START_X=0
   If _START_Y<0 Then _START_Y=0
   If _END_X>=MAP_WIDTH Then _END_X=MAP_WIDTH-1
   If _END_Y>=MAP_HEIGHT Then _END_Y=MAP_HEIGHT-1

   SY=-OFFSET_Y
   For Y=_START_Y To _END_Y
      SX=-OFFSET_X
      For X=_START_X To _END_X
         If MAP(X,Y)>0
            Paste Icon SX,SY,MAP(X,Y)
         End If
         SX=SX+16
      Next X
      SY=SY+16
   Next Y
Return

' ----------------------------

_CREATE_TILES:
Ink 0,0 : Bar 0,0 To 15,15 ' Fondo sólido antes de cada icon

   Ink 2,0 : Bar 0,0 To 15,15 : Get Icon 1,0,0 To 15,15 : Rem Suelo
   Ink 3,0 : Bar 0,0 To 15,15 : Get Icon 2,0,0 To 15,15 : Rem Pared
   Ink 4,0 : Bar 0,0 To 15,15 : Get Icon 3,0,0 To 15,15 : Rem Bloque 1 
   Ink 6,0 : Bar 0,0 To 15,15 : Get Icon 4,0,0 To 15,15 : Rem Bloque 2 
   Ink 5,0 : Bar 0,0 To 15,15 : Get Icon 5,0,0 To 15,15 : Rem Bloque 3 
   Cls 0
Return

' ----------------------------

_CREATE_MAP:
   For Y=0 To MAP_HEIGHT-1
      For X=0 To MAP_WIDTH-1
         If X=0 Or X=MAP_WIDTH-1 Or Y=0 Or Y=MAP_HEIGHT-1
            MAP(X,Y)=2
         Else If Rnd(10)>7
            MAP(X,Y)=Rnd(2)+3
         Else 
            MAP(X,Y)=1
         End If 
      Next X
   Next Y
Return
