' debug screen 
Screen Open 1,320,60,8,Lowres
Curs Off : Flash Off : Hide : Cls 0
Screen Display 1,140,250,320,60
Box 1,1 To 319,59
Ink 2,0

' main game screen 
Screen Open 0,320,200,32,Lowres
Curs Off : Flash Off : Hide : Cls 0

Ink 5,0
Box 1,1 To 319,199
Load "tiles.abk"
Get Icon Palette 

Circle 10,10,5
Paint 11,11
Get Sprite 1,5,5 To 16,16
Cls 0


' ############################ VARIABLES ####################### 
' #### PX,PY coords player, viene del Amal amreg(0) y (1)  
' #### HEADING is the direccion of player (8,4,1,2)
' #### CX,CY coors de la camara
' #### Camara is 230 width,140 height  
' #### CWIDTH,CHEIGHT W and H of the camera  
' #### MOVX detect if player is moving on X, 0 stopped, 1 moving   
' #### MOVY detect if player is moving on Y, 0 stopped, 1 moving   
' #### SWIDTH, SHEIGHT W and H of the whole screen 320*200 
' #### MWIDTH,WHEIGHT map W and H
' #### MAPX,MAPY Start map x,y in for loop 
' ## Player  
Global PX,PY,HEADING,MOVX,MOVY
PX=200 : PY=150
' ## Main screen 
Global SWIDTH,SHEIGHT
SWIDTH=320 : SHEIGHT=200
' ## Visible window dimension and map position 
Global X_TILES,Y_TILES,M_XPOS,M_YPOS
X_TILES=12 : Y_TILES=8
M_XPOS=1 : M_YPOS=1
' ## Whole map dimension 
MWIDTH=32 : MHEIGHT=16
Dim MAP(MWIDTH,MHEIGHT)
Global MAP(),MWIDTH,MHEIGHT
' ## Player limits to move camera
P_RIGHT_LIMIT=214 : P_LEFT_LIMIT=206
P_UP_LIMIT=90 : P_DOWN_LIMIT=130
Global P_RIGHT_LIMIT,P_LEFT_LIMIT,P_UP_LIMIT,P_DOWN_LIMIT
' ### Scroll suave 
Global _SCROLL_X,_SCROLL_Y
_SCROLL_X=0 : _SCROLL_Y=0
' ### Load map tile id in memory 
' ## Not printing here just reserve memory and place values
Gosub _MAPCREATE
Procedure _TEXT
   Text 10,10,"MHEIGHT"+Str$(MHEIGHT)+"   "
   Text 10,20,"MWIDTH"+Str$(MWIDTH)+"   "
   Text 100,10,"PX"+Str$(PX)+"   "
   Text 100,20,"PY"+Str$(PY)+"   "

End Proc
Procedure _MAP_DISPLAY
   SX=-_SCROLL_X : SY=-_SCROLL_Y : Rem <- usamos el offset como punto de partida
   For YY=M_YPOS To(M_YPOS+Y_TILES)
      For XX=M_XPOS To(M_XPOS+X_TILES)
         Paste Icon SX+20,SY+20,MAP(XX,YY)
         SX=SX+16
      Next XX
      SY=SY+16 : SX=-_SCROLL_X
   Next YY
End Proc


Do 
   Screen 1
   _TEXT

   Screen 0
   ' MOVIMIENTO DEL JUGADOR 
   If Jright(1) and PX<300 Then PX=PX+8 : HEADING=8
   If Jleft(1) and PX>40 Then PX=PX-8 : HEADING=4
   If Jdown(1) and PY<180 Then PY=PY+8 : HEADING=2
   If Jup(1) and PY>70 Then PY=PY-8 : HEADING=1

   ' SCROLL HORIZONTAL
   If PX>P_RIGHT_LIMIT and M_XPOS+X_TILES<MWIDTH
      PX=P_RIGHT_LIMIT
      _SCROLL_X=_SCROLL_X+6
      If _SCROLL_X>=16
         _SCROLL_X=0
         Inc M_XPOS
      End If 
   End If 
   If PX<P_LEFT_LIMIT and M_XPOS>1
      PX=P_LEFT_LIMIT
      _SCROLL_X=_SCROLL_X-6
      If _SCROLL_X<0
         _SCROLL_X=16
         Dec M_XPOS
      End If 
   End If 
   ' SCROLL VERTICAL
   If PY>P_DOWN_LIMIT and M_YPOS+Y_TILES<MHEIGHT
      PY=P_DOWN_LIMIT
      _SCROLL_Y=_SCROLL_Y+6
      If _SCROLL_Y>=16
         _SCROLL_Y=0
         Inc M_YPOS
      End If 
   End If 
   If PY<P_UP_LIMIT and M_YPOS>1
      PY=P_UP_LIMIT
      _SCROLL_Y=_SCROLL_Y-6
      If _SCROLL_Y<0
         _SCROLL_Y=16
         Dec M_YPOS
      End If 
   End If 
   ' DIBUJAR MAPA Y PLAYER
   _MAP_DISPLAY

   Sprite 1,PX,PY,1

   Wait Vbl 
Loop 
_MAPCREATE:
Restore MAP1
For Y=1 To MHEIGHT
   For X=1 To MWIDTH
      Read MAP(X,Y)
   Next X
Next Y
MAP1:
Data 4,4,4,4,4,4,4,4,4,4,4,4,4,4,4,4,4,4,4,4,4,4,4,4,4,4,4,4,4,4,4,4
Data 4,4,4,3,3,1,1,1,1,1,1,1,4,4,4,4,1,1,1,1,1,1,1,1,1,1,1,3,3,4,4,4
Data 4,4,1,1,1,1,1,1,1,1,1,1,1,4,4,4,1,1,1,1,1,1,1,1,1,1,1,1,1,3,4,4
Data 4,3,1,1,1,1,1,1,1,1,1,1,1,1,3,3,1,1,1,1,1,1,1,1,1,1,1,1,1,1,3,4
Data 4,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,3,4
Data 4,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,6,1,1,1,1,1,1,1,1,1,1,1,1,1,1,4
Data 4,1,1,1,1,2,2,2,2,2,2,2,2,2,2,2,6,1,1,1,1,1,1,1,1,1,1,1,1,1,1,4
Data 1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,6,1,1,1,1,1,4,1,1,1,1,1,1,1,1,4
Data 4,2,2,2,2,2,3,4,1,1,1,1,1,1,1,1,2,2,2,2,2,2,2,1,1,1,1,1,1,1,1,4
Data 4,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,4
Data 4,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,4
Data 4,1,1,1,1,1,1,1,6,1,1,1,1,1,4,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,4
Data 4,4,1,1,1,1,1,1,6,2,2,2,2,2,2,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,3,4
Data 4,4,1,1,1,1,1,1,6,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,3,4,4
Data 4,4,1,1,1,1,2,2,2,2,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,3,3,3,4,4
Data 4,4,4,4,4,4,4,4,4,4,4,4,4,4,4,4,4,4,4,4,4,4,4,4,4,4,4,4,4,4,4,4
Return 
